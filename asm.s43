#include "msp430.h"                     ; #define controlled include file
        NAME    main                    ; module name
        PUBLIC  main                    ; make the main label vissible
                                        ; outside this module
        ORG     0FFFEh
        DC16    init                    ; set reset vector to 'init' label
        ORG     01C00h                  ; start of ram
//PRE-DEFINED VALUES START HERE
; Digits    0     1     2     3     4     5     6     7     8     9
digitH  db 0xFC, 0x60, 0xDB, 0xF3, 0x67, 0xB7, 0xBF, 0xE0, 0xFF, 0xF7
digitL  db 0x28, 0x20, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00
; Oper      /     +     -     *
operH   db 0x00, 0x03, 0x03, 0x03
operL   db 0x28, 0x50, 0x00, 0xFA
; Error     E     R     O
errorH  db 0x9E, 0xCF, 0xFC
errorL  db 0x00, 0x02, 0x00
//PRE-DEFINED VALUES END HERE
        RSEG    CSTACK                  ; pre-declaration of segment
        RSEG    CODE                    ; place program in 'CODE' segment

init:   mov     #SFE(CSTACK), SP        ; set up stack

main:   nop                             ; main program
        mov     #WDTPW+WDTHOLD,&WDTCTL  ; Stop watchdog timer
        mov.b   #00000110B,&P1REN       ; Activate P1.1 and P1.2 programable
                                        ; pull-up/pull-down resistors
        bis.b   #00000110B,&P1OUT       ; Set resistors for P1.1 and P1.2 as
                                        ; as pull-down
        ; Enable LCD segments
        mov     #0xffff,&LCDCPCTL0
        mov     #0xfc3f,&LCDCPCTL1
        mov     #0x0fff,&LCDCPCTL2
        
        mov     #0x041e,&LCDCCTL0       ; Initialize LCD_C
        mov     #0x0208,&LCDCVCTL       ; Initialize voltage
        mov     #0x8000,&LCDCCPCTL      ; Clock synchronization enabled
        mov     #2,&LCDCMEMCTL          ; Clear LCD memory
        bis     #1,&LCDCCTL0            ; Turn LCD on
        bic     #LOCKLPM5,&PM5CTL0      ; Disable the GPIO power-on by default

// Objetivo:
// Precondiciones:
// Postcondiciones:
// Autor:
// Fecha:
setupCalc:
        mov     #0,R5                   ; Represents the first number the
                                        ; user has inputted 
        mov     #0,R6                   ; Represents the second number the
                                        ; user has inputted 
        mov     #0,R7                   ; Represents the third number the
                                        ; user has inputted 
        mov     #0,R8                   ; Represent the state, the program is on
        mov     #0,R9                   ; Used for knowing the number/operation
                                        ; to display next    
        mov     #0,R10                  ; Represents the first number the
                                        ; user has inputted 
        mov     #0,R11                  ; Represents the second number the
                                        ; user has inputted 
        mov     #0,R12                  ; Represents the third number the
                                        ; user has inputted    
        mov     #0,R14                  ; Represents the first full number the
                                        ; user inputted
        mov     #0,R15                  ; Represents the second full number the
                                        ; user inputted
        mov     #2,&LCDCMEMCTL          ; Clear LCD memory
        call    #displayNumber          ; Display 0 to the user

// Objetivo:
// Precondiciones:
// Postcondiciones:
// Autor:
// Fecha:
mainLoop:                               ; The brains of the application
        mov.b   &P1IN,R4                ; Copy into R4 P1IN's byte
        cmp     #4,R4                   ; Compare if its the left button
        jeq     pressed                 ; Left button was pressed
        cmp     #2,R4                   ; Compares if its the right button
        jeq     pressed                 ; Right button is pressed
        jnz     mainLoop                ; Keep waiting for user input
    
    pressed:                            ; Button was pressed now we wait
        push    R4                      ; Save the button that was pressed
        call    #delay                  ; Delay to help combat button bouncing
        call    #stillHoldDown          ; Verify if the user is still holding 
                                        ; down the button
        pop     R4                      ; Retrieve the button that was pressed

    checkState:                         ; Checks the state of the application
                                        ; and decide what to do next
        cmp     #3,R8                   ; If state is less than 3 means that the 
        jl      incNumberLoop           ; user is still inputting the number
        jeq     incOperationLoop        ; User is now inputting an operation
        cmp     #7,R8                   ; If state is less than 6 means that the
        jl      incNumberLoop           ; user is still inputting the number 
    resetCalc:                          ; User is done with the results now restart
        and.b   #00000110B,R4           ; Compares if the left button (P1.1)
        cmp.b   #00000100B,R4           ; is being pressed
        jeq     setupCalc               ; If true then restart calculator
        cmp.b   #00000010B,R4           ; Compares if the right button (P1.2)
                                        ; is being pressed
        jeq     setupCalc               ; If true then restart calculator
        cmp.b   #00000000B,R4           ; Compares if both button are 
                                        ; being pressed
        jeq     setupCalc               ; If true then restart calculator

    incNumberLoop:                      ; Loop for incrementing the number
        and.b   #00000110B,R4           ; Compares if the left button (P1.1)
        cmp.b   #00000100B,R4           ; is being pressed
        jeq     nextNumber              ; If true then increment the number
        cmp.b   #00000010B,R4           ; Compares if the right button (P1.2)
                                        ; is being pressed
        jeq     nextState               ; User has decided to input next number
        jmp     mainLoop                ; Keep waiting for user input

    incOperationLoop:                   ; Cycles through the operations
        and.b   #00000110B,R4           ; Compares if the left button (P1.1)
        cmp.b   #00000100B,R4           ; is being pressed
        jeq     nextOperation           ; If true then change the operation
        cmp.b   #00000010B,R4           ; Compares if the right button (P1.2)
                                        ; is being pressed
        jeq     operationSelected       ; User has decided the operation, so now
                                        ; we reset to display the next number
        jmp     mainLoop                ; Keep waiting for user input

// Objetivo:
// Precondiciones:
// Postcondiciones:
// Autor:
// Fecha:
nextNumber:                             ; Loops through numbers 0-9
        cmp     #9,R9                   ; Resets the number back to 0 meaning
        jeq     resetNumber             ; the user has accidentaly missed the 
                                        ; desired number
        inc     R9                      ; Increment the number by 1
        call    #displayNumber          ; Display the new number to the user
        jmp     mainLoop                ; Keep waiting for user input

// Objetivo:
// Precondiciones:
// Postcondiciones:
// Autor:
// Fecha:
resetNumber:                            ; Resets the number back to 0
        mov     #0,R9                   ; Number is set back to 0
        call    #displayNumber          ; Display the new number to the user
        jmp     mainLoop                ; Keep waiting for user input

// Objetivo:
// Precondiciones:
// Postcondiciones:
// Autor:
// Fecha:
nextState:                              ; Moves to the next state of the application
        push    R9                      ; Save the number in the stack
        mov     #0,R9                   ; Reset R9
        inc     R8                      ; Increment to the next state
        cmp     #3,R8                   ; If the program is on stage 3 then
        jeq     nextOperation           ; now we display the operation 
        cmp     #7,R8                   ; If the program is on stage 7 then the
        jeq     doTheMath               ; user has finish inputting all the data
                                        ; and is now expecting an answer
        call    #displayNumber          ; Display the next digit to the user
        jmp     mainLoop                ; Keep waiting for user input

// Objetivo:
// Precondiciones:
// Postcondiciones:
// Autor:
// Fecha:
nextOperation:                          ; Loops through operations +,-,*,/
        cmp     #3,R9                   ; Resets the number back to 0 meaning
        jeq     resetOperation          ; the user has accidentaly missed the 
                                        ; desired operation
        inc     R9                      ; Increment to the next operation
        call    #displayOperation       ; Display the next operation to the user
        jmp     mainLoop                ; Keep waiting for user input

// Objetivo:
// Precondiciones:
// Postcondiciones:
// Autor:
// Fecha:
resetOperation:                         ; Reset operation back to +
        mov     #0,R9                   ; Number is set back to 0
        call    #displayOperation       ; Display the next operation to the user
        jmp     mainLoop                ; Keep waiting for user input

// Objetivo:
// Precondiciones:
// Postcondiciones:
// Autor:
// Fecha:
operationSelected:                      ; User has decided the operation
        inc     R8                      ; Increment to the next state
        push    R9                      ; Save the operation in the stack
        mov     #0,R9                   ; Reset R9
        call    #displayNumber          ; Display 0 to the user
        jmp mainLoop                    ; Keep waiting for user input

// Objetivo:
// Precondiciones:
// Postcondiciones:
// Autor:
// Fecha:
stillHoldDown:                          ; User is still holding down the button
        mov.b   &P1IN,R4                ; Copy into R4 P1IN's byte
        and.b   #00000110B,R4           ; Compares which button is being pressed
        cmp.b   #00000100B,R4           ; Left button (P1.1) is being pressed
        jeq     stillHoldDown           ; If true wait for user to stop holding 
                                        ; down the button
        cmp.b   #00000010B,R4           ; Right button (P1.2) is being pressed
        jeq     stillHoldDown           ; If true wait for user to stop holding 
                                        ; down the button
        call    #delay                  ; Delay to help combat button bouncing
        ret                             ; User has finally let go of the button

// Objetivo:
// Precondiciones:
// Postcondiciones:
// Autor:
// Fecha:
delay:                                  ; Used to help against button bouncing
        mov     #0x2710,R13             ; Represents the delay to wait for
                                        ; button bouncing
    decrement:                          ; Label used for looping
        dec     R13                     ; Decrement delay by 1
        jnz     decrement               ; Keep Looping until delay = 0
        ret                             ; Delay finished

// Objetivo:
// Precondiciones:
// Postcondiciones:
// Autor:
// Fecha:
doTheMath:                              ; Mathimatical brain of the application
        pop     R12                     ; Least significant digit of second number
        pop     R11                     ; Second significant digit of second number
        pop     R10                     ; Most significant digit of second number
        pop     R9                      ; Represents the operation
        pop     R7                      ; Least significant digit of first number
        pop     R6                      ; Second significant digit of first number
        pop     R5                      ; Most significant digit of first number
        call    #combineDigits          ; Subroutine for combining digits
        cmp     #1,R9                   ; R9=1 means the operation chosen is add
        jeq     addNumbers              ; Call the add operation
        cmp     #2,R9                   ; R9=2 means the operation chosen is sub
        jeq     subNumbers              ; Call the sub operation
        cmp     #3,R9                   ; R9=3 means the operation chosen is mul
        jeq     mulNumbers              ; Call the mul operation
        cmp     #0,R9                   ; R9=0 means the operation chosen is dev
        jeq     devNumbers              ; Call the dev operation

// Objetivo:
// Precondiciones:
// Postcondiciones:
// Autor:
// Fecha:
combineDigits:                          ; Subroutine for combining digits
    firstDigitCombine:   
        cmp     #0,R5
        jeq     secondDigitCombine
        add     #100, R14               ; This subroutine will add 100 to R14 
        dec     R5                      ; and decrease R5 by one until R5 is 0
        jmp     firstDigitCombine
    secondDigitCombine:     
        cmp     #0,R6
        jeq     thirdDigitCombine
        add     #10, R14                ; This subroutine will add 10 to R14 
        dec     R6                      ; and decrease R6 by one until R6 is 0
        jmp     secondDigitCombine
    thirdDigitCombine:      
        add     R7, R14                 ; This will add the last digit to R14

    firstDigitCombine2:     
        cmp     #0,R10
        jeq     secondDigitCombine2
        add     #100, R15               ; This subroutine will add 100 to R14 
        dec     R10                     ; and decrease R5 by one until R5 is 0
        jmp     firstDigitCombine2
    secondDigitCombine2:    
        cmp     #0,R11
        jeq     thirdDigitCombine2
        add     #10, R15                ; This subroutine will add 100 to R14 
        dec     R11                     ; and decrease R5 by one until R5 is 0
        jmp     secondDigitCombine2
    thirdDigitCombine2:     
        add     R12, R15                ; This will add the last digit to R15
        ret

// Objetivo:
// Precondiciones:
// Postcondiciones:
// Autor:
// Fecha:
addNumbers:                             ; Operation for adding 2 numbers
        add     R15,R14                 ; Adds numbers in R14 and R15 and
                                        ; stores the result in R14
        mov     #0,R15                  ; Set R15 to 0 because we need it later
        cmp     #0,R14                  ; Check if the result = 0
        jeq     display0                ; If result = 0 then display 0
        jmp     seperateDigits          ; Seperate the digits into diferent registers
        jmp     mainLoop                ; Keep waiting for user input

// Objetivo:
// Precondiciones:
// Postcondiciones:
// Autor:
// Fecha:
mulNumbers:                             ; Operation for multiplying 2 numbers
        cmp     #0,R15                  ; Check if the R15 = 0
        jeq     display0                ; If R15 = 0 then result = 0 
        cmp     #0,R14                  ; Check if the R14 = 0
        jeq     display0                ; If R14 = 0 then result = 0 
    multSetup:                          ; Setup numbers in the proper order
                                        ; for multiplication
        cmp     R14, R15                ; Compare which number is bigger
        jl      multAlgorithm           ; If R14>R15 then multiply both numbers
        call    #multSwitchNumbers      ; If R15>R14 then reverse both numbers
    multAlgorithm:                      ; Algorithm for multiplying 2 numbers
        mov     R14, R6                 ; Re-aranging data for multiplication
        mov     R15, R7                 ; Re-aranging data for multiplication
        mov     #0, R15                 ; Set R15 to 0 because we need it later
    multLoop:                           ; Lable used for looping
        dec     R7                      ; Decrement by 1
        jz      multDone                ; Multiplication is finished
        add     R6, R14                 ; Remember that multiplication is
                                        ; just a series of additions
        cmp     #10000, R14             ; If R14 is greater or equal than 1000
        jge     multReachedThousand     ; save that thousand on another register
        jmp     multLoop                ; Multiplication not done
    multDone:                           ; Multiplication finished
        jmp     seperateDigits          ; Seperate the digits into diferent registers
        jmp     mainLoop                ; Keep waiting for user input

// Objetivo:
// Precondiciones:
// Postcondiciones:
// Autor:
// Fecha:
multReachedThousand:                    ; Save that thousand on R15
        sub     #10000,R14              ; Sub 1000-R14 so we can move that to R15
        inc     R15                     ; Increment 1 meaning the first thousand
        jmp     multLoop                ; Go back to multiplying

// Objetivo:
// Precondiciones:
// Postcondiciones:
// Autor:
// Fecha:
multSwitchNumbers:                      ; Invert the data on both R14 and R15
        push     R15                    ; Pushed to the stack so we dont 
                                        ; override the data
        mov      R14, R15               ; Swap R14 to R15
        pop      R14                    ; Retrieve the data from the stack
        ret                             ; Swapping done

// Objetivo:
// Precondiciones:
// Postcondiciones:
// Autor:
// Fecha:
subNumbers:                             ; Operation for subtracting 2 numbers
        sub     R15, R14                ; R14 = R14 - R15
        mov     #0,R15                  ; Set R15 to 0 because we need it later
        cmp     #0,R14                  ; Check if the result = 0
        jeq     display0                ; If result = 0 then display 0
        jmp     seperateDigits          ; Seperate the digits into diferent registers
        jmp     mainLoop                ; Keep waiting for user input

// Objetivo:
// Precondiciones:
// Postcondiciones:
// Autor:
// Fecha:
devNumbers:                             ; Operation for deviding 2 numbers
        cmp     #0,R15                  ; Check if the denominator is 0
        jeq     ceroIsOnDenominator     ; If 0 then show error
        cmp     #0,R14                  ; Check if the numerator is 0
        jeq     ceroIsOnNumerator       ; If 0 then result = 0
        mov     #0, R5                  ; R5 = quotient = 0   
        
    divLoop:                            ; Lable used for looping
        sub     R15, R14                ; R14 = R14 - R15
        cmp     #0, R14                 ; Compare to see if we can keep subtracting
        jl      divEnd                  ; R14 < 0, its negative so we subtracted
                                        ; too much
        inc     R5                      ; Quotient += 1
        jmp     divLoop                 ; Keep dividing    
   
    divEnd:                             ; Division finished
        mov     R5, R14                 ; Save result in R14
        mov     #0,R15                  ; Set R15 to 0 because we need it later
        cmp     #0,R14                  ; Check if the result = 0
        jeq     display0                ; If result = 0 then display 0
        jmp     seperateDigits          ; Seperate the digits into diferent registers
        jmp     mainLoop                ; Keep waiting for user input

// Objetivo:
// Precondiciones:
// Postcondiciones:
// Autor:
// Fecha:
ceroIsOnNumerator:                      ; Cero is on numerator meaning result=0
        mov     #2,&LCDCMEMCTL          ; Clear LCD memory
        call    #displayFirstSegment    ; Display number to user
        jmp     mainLoop                ; Keep waiting for user input

// Objetivo:
// Precondiciones:
// Postcondiciones:
// Autor:
// Fecha:
ceroIsOnDenominator:                    ; Cero is on denominator meaning an error
        call    #displayError           ; Display error to user
        jmp     mainLoop                ; Keep waiting for user input

// Objetivo: Muestra un cero en el primer segmento
// Precondiciones: R9 representa el número a ser mostrado
// Postcondiciones: R9 siempre será cero al final
// Autor: Rey J. Cotto
// Fecha: 13/03/21
display0:                               ; Display the number 0 to the user
        mov     #0,R9                   ; R9 represents the number to be printed
        call    displayFirstSegment     ; Display the number on the first segment
        jmp     mainLoop                ; Keep waiting for user input 

// Objetivo:
// Precondiciones:
// Postcondiciones:
// Autor:
// Fecha:
seperateDigits:                         ; Separeates R14 into its decimal components
        cmp     #2, R9                  ; Subtraction, always 3 digits, but sometimes
        jeq     sepDigSub               ; it's negative so we have to account for that
        jmp     seperate                ; Any other operation gets seperated the same way

// Objetivo:
// Precondiciones:
// Postcondiciones:
// Autor:
// Fecha:
sepDigSub:                              ; Checks if R14 is negative
        push    R14                     ; Store R14 in stack
        and     #1000000000000000b, R14 ; Apply bit mask
                                        ; If most significant bit is 1, then its a negative number
        cmp     #0,R14                  ; If R14 == 0, then most significant bit is 0
        pop     R14                     ; Restore value of R14
        jeq     seperate                ; R14 is a positive number
        jmp     applyNegative           ; R14 is a negative number

// Objetivo:
// Precondiciones:
// Postcondiciones:
// Autor:
// Fecha:
applyNegative: 
        inv     R14                     ; Convert R14 to positive, first invert everything,
        inc     R14                     ; Then add 1
        mov     #1,R13                  ; Add negative flag
        jmp     seperate                ; Seperate the digits

// Objetivo:
// Precondiciones:
// Postcondiciones:
// Autor:
// Fecha:
seperate:                               ; Subroutine used for seperating the 
                                        ; results found in R14 and R15 (if any) 
        mov     #3,R12                  ; R12 serves as a counter for seperating
                                        ; each digit. At first it removes whats 
                                        ; inside R15
        mov     #0,R11                  ; R11 serves as a counter for seperating
                                        ; 6 digits
    loop:                               ; Lable used for looping
        dec     R12                     ; Decrements by 1 meaning seperate to 
                                        ; the next lower tenses 
        mov     #0, R9                  ; R9 = quotient = 0
        cmp     #1,R12                  ; R12=1 means seperate by ones
        jeq      ones                   ; Seperate ones
        cmp     #2,R12                  ; R12=2 means seperate by tens
        jeq      tens                   ; Seperate tens
        cmp     #3,R12                  ; R12=2 means seperate by hundreds
        jeq     hundreds                ; Seperate hundreds
        cmp     #0,R12                  ; R12=0 means it's done seperating
        jeq     otherRegister           ; Switch whats inside register R14 to R15
                                        ; or it means that seperation is finished
        jmp     thousands               ; R12=4 means seperate by thousands
        
    otherRegister:                     
        cmp     #6,R11                  ; R11=6 means that 6 digits where seperated
        jeq     finished                ; Seperation of digits is completed 
        mov     R14,R15                 ; Switch what is in R14 to R15
        mov     #0,R14                  ; Clear R14 because the data is now on R15
        mov     #5,R12                  ; R12=5 meaning that it should loop 4 
                                        ; more times so we can seperate what is inside R14
        jmp     loop                    ; Keep looping
    
    ones:                               ; If we are seperating the ones then
        mov     R15,R9                  ; move R15 to R9 because its already in 
                                        ; the tense form
        mov     #0,R15                  ; Clear R15 for later use
        jmp     pushDigit               ; Push the digit on the stack

    tens:                               ; If we are seperating the tens then
        mov     #10,R4                  ; set R4 to 10 so we can subtract by tens
        jmp     subWithTenses           ; Sub R15 at the specified tens
        
    hundreds:                           ; If we are seperating the hundreds then
        mov     #100,R4                 ; set R4 to 100 so we can subtract by hundreds
        jmp     subWithTenses           ; Sub R15 at the specified tens
        
    thousands:                          ; If we are seperating the thousands then
        mov     #1000,R4                ; set R4 to 1000 so we can subtract by thousands
        jmp     subWithTenses           ; Sub R15 at the specified tens
    
        
    subWithTenses:                      ; General subroutine to seperate digits
                                        ; by the specified tens
        cmp     R4,R15                  ; R4<R5 means that the number is a lower tens
        jl      pushDigit               ; Push the digit to the stack
        sub     R4,R15                  ; Sub R4-R15 by the specified tens
        inc     R9                      ; Increment R9 by 1 meaning its the quotient
        jmp     subWithTenses           ; Keep subtracting by the specified tens
    
    pushDigit:                          ; Push digit on the stack for later use
        push    R9                      ; Push R9 = quotient = seperated digit
        inc     R11                     ; Increment R11 by 1 meaning we have 1 more saved digit
        jmp     loop                    ; Restart the loop again

    finished:                           ; Seperation of digits is completed
      ; This is done to reverse the stack for proper display of the results
        pop     R12
        pop     R11
        pop     R10
        pop     R7
        pop     R6
        pop     R5
        push    R12
        push    R11
        push    R10
        push    R7
        push    R6
        push    R5
        jmp     displayResults          ; Display results to the user

// Objetivo: Subrutina diseñada para mostrar los números resultantes en la pantalla, en su posición adecuada
// Precondiciones: R10 representa el número resultante
// Postcondiciones: El número va a estar mostrado en la pantalla
// Autor: Rey J. Cotto
// Fecha: 14/03/21
displayResults:                         ; Subroutine used for displaying the results
        mov     #7,R10                  ; R10 represents the 6 digits we are going to display
        mov     #2,&LCDCMEMCTL          ; Clear LCD memory
        mov     #0,R11                  ; R11 is used as a flag for if digit has 
                                        ; 0 on the left side of the result, but 
                                        ; it also serves as an indicator as to 
                                        ; were to display the number
    displayNextDigit:                   ; Lable used for looping the next digit to display
        dec     R10                     ; Decrement R10 by 1 meaning a digit is
                                        ; being displayed
        cmp     #0,R10                  ; R10=0 it means that all digits have been displayed
        jeq     resultsDone             ; Result has finally finished printing
        pop     R9                      ; Pop the digit from the stack
        cmp     #1,R11                  ; If R11 >= 1 then that means that their are
        jge     noMoreLeftZeros         ; no mores zeros to the left of the number
        cmp     #0,R9                   ; If R9=0 that means that a zero was
        jeq     displayNextDigit        ; found on the left of the digit so skip
    noMoreLeftZeros:                    ; No more zeros found on the left side
        inc     R11                     ; Increment R11 by 1 to display the next
                                        ; digit on the next display segment
        cmp     #1,R11                  ; R11=1 means that the first display
                                        ; segment can be used for displaying the digit
        jeq     displayFirstDigit       ; Display digit on the first segment
        cmp     #2,R11                  ; R11=2 means that the second display
                                        ; segment can be used for displaying the digit
        jeq     displaySecondDigit      ; Display digit on the second segment
        cmp     #3,R11                  ; R11=3 means that the third display
                                        ; segment can be used for displaying the digit
        jeq     displayThirdDigit       ; Display digit on the third segment
        cmp     #4,R11                  ; R11=4 means that the fourth display
                                        ; segment can be used for displaying the digit
        jeq     displayFourthDigit      ; Display digit on the fourth segment
        cmp     #5,R11                  ; R11=5 means that the fifth display
                                        ; segment can be used for displaying the digit
        jeq     displayFifthDigit       ; Display digit on the fifth segment
        cmp     #6,R11                  ; R11=6 means that the sixth display
                                        ; segment can be used for displaying the digit
        jeq     displaySixthDigit       ; Display digit on the sixth segment
        
    displayFirstDigit:                  ; Display digit on the first segment
        call    #displayFirstSegment    ; Call subroutine for displaying digit 
                                        ; on the first segment display
        cmp     #1,R13                  ; R13 represents a flag meaning if the
                                        ; number is negative (1) or positive (0)
        jeq     displayNegative         ; If it's a negative number then display
                                        ; the negative sign
        jmp     displayNextDigit        ; Go to the next digit for displaying
        
    displaySecondDigit:                 ; Display digit on the second segment
        call    #displaySecondSegment   ; Call subroutine for displaying digit 
                                        ; on the second segment display
        jmp     displayNextDigit        ; Go to the next digit for displaying
        
    displayThirdDigit:                  ; Display digit on the Third segment
        call    #displayThirdSegment    ; Call subroutine for displaying digit 
                                        ; on the Third segment display
        jmp     displayNextDigit        ; Go to the next digit for displaying
        
    displayFourthDigit:                 ; Display digit on the Fourth segment
        call    #displayFourthSegment   ; Call subroutine for displaying digit 
                                        ; on the Fourth segment display
        jmp     displayNextDigit        ; Go to the next digit for displaying
        
    displayFifthDigit:                  ; Display digit on the Fifth segment
        call    #displayFifthSegment    ; Call subroutine for displaying digit 
                                        ; on the Fifth segment display
        jmp     displayNextDigit        ; Go to the next digit for displaying
        
    displaySixthDigit:                  ; Display digit on the Sixth segment
        call    #displaySixthSegment    ; Call subroutine for displaying digit 
                                        ; on the Sixth segment display
        jmp     displayNextDigit        ; Go to the next digit for displaying
        
    resultsDone:                        ; Result has finally finished printing
        jmp     mainLoop                ; Keep waiting for user input

// Objetivo: Subrutina diseñada para mostrar el signo negativo
// Precondiciones: El negative flag fue activado y entró a la subrutina
// Postcondiciones: El simbolo de negativo está mostrado en la pantalla
// Autor: Rey J. Cotto
// Fecha: 14/03/21
displayNegative:                        ; Display the negative sign found on the
                                        ; first segment display
        bis.b   #4,&0x0A29+1            ; Code used for turning on the - sign on
                                        ; the first segment display
        jmp     displayNextDigit        ; Go to the next digit for displaying

// Objetivo: Subrutina diseñada para mostrar los números no finales, en su posición adecuada
// Precondiciones: R8 representa el estado del programa
// Postcondiciones: Alguna subrutina va a ser llamada al final como resultado de esta
// Autor: Rey J. Cotto
// Fecha: 14/03/21
displayNumber:                          ; Subroutine used for displaying numbers
                                        ; based on the state the program is in
        cmp     #0,R8                   ; If R8 is at state 0 then display the 
        jeq     displayFirstSegment     ; number on the first segment display
        cmp     #1,R8                   ; If R8 is at state 1 then display the
        jeq     displaySecondSegment    ; number on the second segment display
        cmp     #2,R8                   ; If R8 is at state 2 then display the
        jeq     displayThirdSegment     ; number on the third segment display
        cmp     #3,R8                   ; If R8 is at state 3 then display the
        jeq     displayOperation        ; Operation on the first segment display
        cmp     #4,R8                   ; If R8 is at state 4 then display the
        jeq     displayFirstSegment     ; number on the first segment display
        cmp     #5,R8                   ; If R8 is at state 5 then display the
        jeq     displaySecondSegment    ; number on the second segment display
        cmp     #6,R8                   ; If R8 is at state 6 then display the
        jeq     displayThirdSegment     ; number on the third segment display

// Objetivo: Mostrar la operación a computar en la pantalla
// Precondiciones: R9 representa la posición en la lista
// Postcondiciones: El símbolo va a ser mostrado en la pantalla
// Autor: Rey J. Cotto
// Fecha: 14/03/21
displayOperation:                       ; Display the operation based on R9
        mov     #2,&LCDCMEMCTL          ; Clear LCD memory
        mov.b   operH(R9),&0x0A29       ; R9 represents the position fo the list
                                        ; of hexadecimal numbers that represents
        mov.b   operL(R9),&0x0A29+1     ; the operations such as +,-,*,/
        ret                             ; Display done

// Objetivo: 
// Precondiciones: 
// Postcondiciones: 
// Autor: 
// Fecha:
displayError:                           ; This display an error message when
                                        ; user decides to devide by 0
        mov     #2,&LCDCMEMCTL          ; Clear LCD memory
        mov     #0,R9                   ; R9 represents the position of the list
        mov.b   errorH(R9),&0x0A29      ; Display the letter E
        mov.b   errorL(R9),&0x0A29+1    ; Display the letter E
        mov     #1,R9                   ; R9 represents the position of the list
        mov.b   errorH(R9),&0x0A25      ; Display the letter R
        mov.b   errorL(R9),&0x0A25+1    ; Display the letter R
        mov     #1,R9                   ; R9 represents the position of the list
        mov.b   errorH(R9),&0x0A23      ; Display the letter R
        mov.b   errorL(R9),&0x0A23+1    ; Display the letter R
        mov     #2,R9                   ; R9 represents the position of the list
        mov.b   errorH(R9),&0x0A32      ; Display the letter O
        mov.b   errorL(R9),&0x0A32+1    ; Display the letter O
        mov     #1,R9                   ; R9 represents the position of the list
        mov.b   errorH(R9),&0x0A2E      ; Display the letter R
        mov.b   errorL(R9),&0x0A2E+1    ; Display the letter R
        jmp     mainLoop                ; Keep waiting for user input

// Objetivo: Subrutina diseñada para mostrar en el primer segmento del LCD
// Precondiciones: R9 contiene la posición a ser mostrada
// Postcondiciones: Se estará mostrando lo indicado en el primer segmento
// Autor: Rey J. Cotto
// Fecha: 14/03/21
displayFirstSegment:                    ; Soubroutine used for displaying on the
                                        ; first segment display
        mov.b   digitH(R9),&0x0A29      ; Display the high byte of digitH at position R9
        mov.b   digitL(R9),&0x0A29+1
        ret

// Objetivo: Subrutina diseñada para mostrar en el segundo segmento del LCD
// Precondiciones: R9 contiene la posición a ser mostrada
// Postcondiciones: Se estará mostrando lo indicado en el segundo segmento
// Autor: Rey J. Cotto
// Fecha: 14/03/21
displaySecondSegment:                   ; Soubroutine used for displaying on the
                                        ; first segment display
        mov.b   digitH(R9),&0x0A25      ; Display the high byte of digitH at position R9
        mov.b   digitL(R9),&0x0A25+1
        ret

// Objetivo: Subrutina diseñada para mostrar en el tercer segmento del LCD
// Precondiciones: R9 contiene la posición a ser mostrada
// Postcondiciones: Se estará mostrando lo indicado en el tercer segmento
// Autor: Rey J. Cotto
// Fecha: 14/03/21
displayThirdSegment:                    ; Soubroutine used for displaying on the
                                        ; first segment display
        mov.b   digitH(R9),&0x0A23      ; Display the high byte of digitH at position R9
        mov.b   digitL(R9),&0x0A23+1
        ret

// Objetivo: Subrutina diseñada para mostrar en el cuarto segmento del LCD
// Precondiciones: R9 contiene la posición a ser mostrada
// Postcondiciones: Se estará mostrando lo indicado en el cuarto segmento
// Autor: Rey J. Cotto
// Fecha: 14/03/21
displayFourthSegment:                   ; Soubroutine used for displaying on the
                                        ; first segment display
        mov.b   digitH(R9),&0x0A32      ; Display the high byte of digitH at position R9
        mov.b   digitL(R9),&0x0A32+1    ; Display the low byte of digitL at position R9
        ret

// Objetivo: Subrutina diseñada para mostrar en el quinto segmento del LCD
// Precondiciones: R9 contiene la posición a ser mostrada
// Postcondiciones: Se estará mostrando lo indicado en el quinto segmento
// Autor: Rey J. Cotto
// Fecha: 14/03/21
displayFifthSegment:                    ; Soubroutine used for displaying on the
                                        ; first segment display
        mov.b   digitH(R9),&0x0A2E      ; Display the high byte of digitH at position R9
        mov.b   digitL(R9),&0x0A2E+1    ; Display the low byte of digitL at position R9
        ret

// Objetivo: Subrutina diseñada para mostrar en el sexto segmento del LCD
// Precondiciones: R9 contiene la posición a ser mostrada
// Postcondiciones: Se estará mostrando lo indicado en el sexto segmento
// Autor: Rey J. Cotto
// Fecha: 14/03/21
displaySixthSegment:                    ; Soubroutine used for displaying on the
                                        ; first segment display
        mov.b   digitH(R9),&0x0A27      ; Display the high byte of digitH at position R9
        mov.b   digitL(R9),&0x0A27+1    ; Display the low byte of digitL at position R9
        ret

fin:    JMP $                           ; jump to current location '$'
                                        ; (endless loop)
        END
